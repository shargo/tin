program = line+ LINE_START

<line> = LINE_START (statement_call | expression)

statement_call = symbol expression block?

block = INDENT line+ keyword_continuation* DEDENT

<keyword_continuation> = (DEDENT LINE_START keyword expression? INDENT line+)

<expression> = fncall_expression | operator_call

operator_call = fncall_expression (operator fncall_expression)+

<fncall_expression> = property_expression | function_call

function_call = callable_expression LPAREN (expression (COMMA expression)*)? RPAREN block?

<callable_expression> = grouping_expression | fncall_expression

<property_expression> = atom | property_call

property_call = callable_expression DOT symbol

<atom> = grouping_expression | symbol | number | keyword

<grouping_expression> = LPAREN expression RPAREN

<symbol> = <'SYMBOL('> SYMBOL <')'>

SYMBOL = #'[^)]*'

<keyword> = <'KEYWORD('> KEYWORD <')'>

KEYWORD =  #'[^)]*'

<operator> = <'OPERATOR('> OPERATOR <')'>

OPERATOR =  #'[^)]*'

<number> = <'NUMBER('> NUMBER <')'>

NUMBER =  #'[^)]*'

<DOT> = <'DOT'>

<COMMA> = <'COMMA'>

<LPAREN> = <'LPAREN'>

<RPAREN> = <'RPAREN'>

<LINE_START> = <'LINE_START'>

<INDENT> = <'INDENT'>

<DEDENT> = <'DEDENT'>
